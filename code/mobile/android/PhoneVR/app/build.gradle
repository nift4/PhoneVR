import com.android.build.gradle.internal.dsl.PackagingOptions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply from: '../versioning.gradle'
apply plugin: "com.diffplug.spotless"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def defaultALVRFlavour = "--release"

android {
    signingConfigs {
        releaseconfig {
            if( System.getenv('GITHUB_EVENT_NAME') == 'pull_request' || System.getenv("Store") == null || System.getenv("Key") == null ) {
                // If its a PR Supply a dummy KeySigningConfig
                // Or in Local env if Store and Keys are not present in ENV Vars do the same
                storeFile file("PhoneVRKeyStorePR.jks")
                storePassword "test1234"
                keyAlias "PhoneVRKeyPR"
                keyPassword "test1234"
                println "[PVR] Configuring releaseconfigs for testing a PR..."
            }
            else {
                storeFile file(keystoreProperties['storeFile'])
                storePassword System.getenv("Store")
                keyAlias keystoreProperties['keyAlias']
                keyPassword System.getenv("Key")
                println "[PVR] Configuring releaseconfigs for release (push) (not a PR)"
            }
        }
    }
    compileSdkVersion 34
    namespace 'viritualisres.phonevr'
    ndkVersion '25.2.9519653' // r25c
    defaultConfig {
        applicationId "viritualisres.phonevr"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode buildVersionCode()
        versionName buildVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments useTestStorageService: 'true'

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -fexceptions -frtti" //-DANDROID_USE_LEGACY_TOOLCHAIN_FILE=ON"
            }
        }

        defaultPublishConfig 'release'
        publishNonDefault true
        archivesBaseName = "PhoneVR-v$versionName"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.releaseconfig
        }
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    cppFlags += "-D_DEBUG=1"
                }
            }
            defaultALVRFlavour = ""
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding true
    }
    flavorDimensions "gvr"
    productFlavors {
        gvr {
            dimension "gvr"
            externalNativeBuild {
                cmake {
                    arguments "-D HAVEGVR=true"
                }
            }
        }
        noGvr {
            dimension "gvr"
            versionNameSuffix "-lite"
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libraries/jni", "../ALVR/build/alvr_client_core"]
        }
    }
    externalNativeBuild {
        cmake {
            // version "3.6.4"
            path "CMakeLists.txt"
        }
    }
    project.gradle.taskGraph.whenReady {
        android.productFlavors.all { flavor ->
            // Capitalize (as Gralde is case-sensitive).
            def flavorName = flavor.name.substring(0, 1).toUpperCase() + flavor.name.substring(1)

            // At last, configure.
            "connected${flavorName}DebugAndroidTest" {
                ignoreFailures = true
            }
        }
    }

    // CompileOptions required for ARCA
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        gradle.startParameter.getTaskNames().each { task ->
            println("[PVR] task is " + task)
            if (task.contains('debug') || task.contains('Debug')) {
                def android = project.extensions.findByName("android")

                if(android != null) {
                    PackagingOptions packagingOptions = android["packagingOptions"]

                    if (packagingOptions != null) {
                        packagingOptions.doNotStrip = [ "**/*.so" ]
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.6.1'

    implementation "androidx.tracing:tracing:1.1.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test.ext:junit-ktx:$extJUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "androidx.test:rules:$androidXTestVersion"
    androidTestUtil "androidx.test.services:test-services:$servicesVersion"

    gvrImplementation 'com.google.vr:sdk-base:1.200.0@aar'
    implementation 'com.google.cardboard.sdk:cardboard-sdk:1.17.0@aar'
    implementation 'io.github.zxing-cpp:android:2.2.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.19.4'
    implementation 'androidx.camera:camera-core:1.3.4'
    implementation 'androidx.camera:camera-view:1.3.4'
    implementation 'androidx.camera:camera-lifecycle:1.3.4'
    implementation 'androidx.camera:camera-camera2:1.3.4'

    def acraVersion = '5.7.0'
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
}

// The dependencies for NDK builds live inside the .aar files so they need to
// be extracted before NDK targets can link against.
task extractNdk(type: Copy)  {
    copy {
        from zipTree("../cardboard/cardboard-sdk.aar")
        into "libraries/"
        include "jni/**/libGfxPluginCardboard.so"
    }
    if (file("../gvr-android-sdk-1.200/libraries/sdk-base-1.200.0.aar").exists()) {
        copy {
            from zipTree("../gvr-android-sdk-1.200/libraries/sdk-base-1.200.0.aar")
            into "libraries/"
            include "headers/vr/gvr/capi/**/*h"
            include "jni/**/libgvr_audio.so"
            include "jni/**/libgvr.so"
        }
    }
}

task deleteNdk(type: Delete) {
    delete "libraries/"
    delete "../ALVR/build/"
    delete "../ALVR/target/"
}

task buildClientLib {
    doLast {
        exec {
            workingDir '../ALVR'
            if(defaultALVRFlavour.length() > 0)
                commandLine 'cargo', 'xtask', 'build-client-lib', '--no-stdcpp', defaultALVRFlavour
            else
                commandLine 'cargo', 'xtask', 'build-client-lib', '--no-stdcpp'
        }
    }
}

// run /src/androidTest/java/viritualisres/phonevr/utils/pvr-adb-telnet.sh before starting connectedAndroidTest
// TODO: Automate with gradle tasks
//connectedAndroidTest.dependsOn(runPVRADBTelnetServer)

spotless {
    java {
        // don't need to set target, it is inferred from java
        // fix formatting of type annotations
        target '**/*.java'
        formatAnnotations()
        removeUnusedImports()
        // apply a specific flavor of google-java-format
        googleJavaFormat().aosp().reflowLongStrings()
        // make sure every file has the following copyright header.
        // optionally, Spotless can set copyright years by digging
        // through git history (see "license" section below)
        licenseHeader '/* (C)$YEAR */'
    }
    kotlin {
        // by default the target is every '.kt' and '.kts` file in the java sourcesets
        target '**/*.kt'
        ktfmt().dropboxStyle()    // has its own section below
        licenseHeader '/* (C)$YEAR */' // or licenseHeaderFile
    }
}

preBuild.dependsOn(buildClientLib)
preBuild.dependsOn("spotlessCheck")

build.dependsOn(extractNdk)
build.dependsOn(buildClientLib)

clean.dependsOn(deleteNdk)
